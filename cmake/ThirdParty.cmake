include("${CMAKE_CURRENT_LIST_DIR}/GetCPM.cmake")

set(BUILD_SHARED_LIBS OFF CACHE BOOL "Build dependencies as static libraries" FORCE)
set(TAILGATE_ORIGINAL_C_FLAGS "${CMAKE_C_FLAGS}")
set(TAILGATE_ORIGINAL_CXX_FLAGS "${CMAKE_CXX_FLAGS}")
string(APPEND CMAKE_C_FLAGS " ${TAILGATE_THIRD_PARTY_COMPILE_FLAGS}")
string(APPEND CMAKE_CXX_FLAGS " ${TAILGATE_THIRD_PARTY_COMPILE_FLAGS}")

# Warning-specific exceptions survive upstream's later -Werror; -U cancels its private debug ABI.
# The quoted port headers must take precedence over the example's -I paths.
string(
    CONCAT
    TAILGATE_LWIP_C_FLAGS
    "${CMAKE_C_FLAGS} -ULWIP_DEBUG"
    " -Wno-error=unreachable-code"
    " -Wno-error=unused"
    " -Wno-error=unused-parameter"
    " -iquote \"${PROJECT_SOURCE_DIR}/src/core/wgengine/netstack/port\""
)

if(TAILGATE_BUILD_LINUX)
    CPMAddPackage(
        NAME c-ares
        VERSION 1.34.6
        URL https://github.com/c-ares/c-ares/releases/download/v1.34.6/c-ares-1.34.6.tar.gz
        URL_HASH SHA256=912dd7cc3b3e8a79c52fd7fb9c0f4ecf0aaa73e45efda880266a2d6e26b84ef5
        OPTIONS
            "CARES_STATIC ON"
            "CARES_SHARED OFF"
            "CARES_BUILD_TOOLS OFF"
            "CARES_BUILD_TESTS OFF"
            "CARES_INSTALL OFF"
    )
endif()

CPMAddPackage(
    NAME Lwip
    URL https://codeload.github.com/lwip-tcpip/lwip/tar.gz/refs/tags/STABLE-2_2_1_RELEASE
    URL_HASH SHA256=ce0b7461c0ad9602c376f0bf07c5eb7253b48c7bf66f011c6bf3e2a96731c539
    EXCLUDE_FROM_ALL YES
    OPTIONS "CMAKE_C_FLAGS ${TAILGATE_LWIP_C_FLAGS}"
)
unset(TAILGATE_LWIP_C_FLAGS)

CPMAddPackage(
    NAME Expat
    VERSION 2.8.4
    URL https://github.com/libexpat/libexpat/releases/download/R_2_8_4/expat-2.8.4.tar.bz2
    URL_HASH SHA256=963250a823c16a498582b4ad82ad0f88926be0769675d3b6956be4d769a1cd8f
    OPTIONS
        "EXPAT_SHARED_LIBS OFF"
        "EXPAT_BUILD_TOOLS OFF"
        "EXPAT_BUILD_EXAMPLES OFF"
        "EXPAT_BUILD_TESTS OFF"
        "EXPAT_BUILD_DOCS OFF"
        "EXPAT_BUILD_PKGCONFIG OFF"
        "EXPAT_ENABLE_INSTALL OFF"
        "EXPAT_DTD OFF"
        "EXPAT_GE OFF"
)

# Consume generated C sources so builds do not require Node.js or npm code generation.
CPMAddPackage(
    NAME Llhttp
    VERSION 9.4.3
    URL https://github.com/nodejs/llhttp/archive/refs/tags/release/v9.4.3.tar.gz
    URL_HASH SHA256=1eb813c7437b31a87496a1cd3ed79f00746720f5e7e29c79b42c02cb69f36c39
    SYSTEM YES
    OPTIONS "LLHTTP_BUILD_SHARED_LIBS OFF" "LLHTTP_BUILD_STATIC_LIBS ON"
)

CPMAddPackage(
    NAME Sodium
    GIT_REPOSITORY https://github.com/robinlinden/libsodium-cmake.git
    GIT_TAG e5b985ad0dd235d8c4307ea3a385b45e76c74c6a
    OPTIONS "SODIUM_DISABLE_TESTS ON"
)

# Upstream requires this include even though the opaque handle types come from definitions.
# Keep the dependency independent of Tailgate source headers.
file(
    CONFIGURE
    OUTPUT "${PROJECT_BINARY_DIR}/generated/mbedtls-threading/threading_alt.h"
    CONTENT ""
)
# The optional ECC self-tests increment unsynchronized global operation counters even during
# ordinary TLS handshakes. Tailgate does not call the self-test entry points.
file(
    CONFIGURE
    OUTPUT "${PROJECT_BINARY_DIR}/generated/mbedtls-threading/crypto_config.h"
    CONTENT "#undef MBEDTLS_SELF_TEST\n"
)

# Avoid git for MbedTLS
# The source repository has large submodules and requires other third-party tools for codegen.
CPMAddPackage(
    NAME MbedTLS
    VERSION 4.1.0
    URL https://github.com/Mbed-TLS/mbedtls/releases/download/mbedtls-4.1.0/mbedtls-4.1.0.tar.bz2
    URL_HASH SHA256=377a09cf8eb81b5fb2707045e5522d5489d3309fed5006c9874e60558fc81d10
    OPTIONS
        "ENABLE_PROGRAMS OFF"
        "ENABLE_TESTING OFF"
        "TF_PSA_CRYPTO_USER_CONFIG_FILE crypto_config.h"
        "CMAKE_C_FLAGS ${CMAKE_C_FLAGS} \
        -DMBEDTLS_THREADING_C \
        -DMBEDTLS_THREADING_ALT \
        -D\"mbedtls_platform_mutex_t=void*\" \
        -D\"mbedtls_platform_condition_variable_t=void*\" \
        -I\"${PROJECT_BINARY_DIR}/generated/mbedtls-threading\""
)
# Consumers must compile the public crypto structures with the same opaque handle layout.
target_compile_definitions(
    tfpsacrypto
    INTERFACE
        MBEDTLS_THREADING_C
        MBEDTLS_THREADING_ALT
        "mbedtls_platform_mutex_t=void*"
        "mbedtls_platform_condition_variable_t=void*"
)
target_include_directories(
    tfpsacrypto
    INTERFACE "$<BUILD_INTERFACE:${PROJECT_BINARY_DIR}/generated/mbedtls-threading>"
)

CPMAddPackage(
    NAME NlohmannJson
    VERSION 3.12.0
    GIT_REPOSITORY https://github.com/nlohmann/json.git
    GIT_TAG v3.12.0
    OPTIONS "JSON_BuildTests OFF"
)

CPMAddPackage(
    NAME Zint
    VERSION 2.16.0
    GIT_REPOSITORY https://github.com/zint/zint.git
    GIT_TAG 2.16.0
    GIT_SHALLOW TRUE
    OPTIONS
        "ZINT_SHARED OFF"
        "ZINT_STATIC ON"
        "ZINT_FRONTEND OFF"
        "ZINT_USE_GS1SE OFF"
        "ZINT_USE_PNG OFF"
        "ZINT_USE_QT OFF"
        "ZINT_TEST OFF"
        "ZINT_UNINSTALL OFF"
)

CPMAddPackage(
    NAME CLI11
    VERSION 2.6.2
    GIT_REPOSITORY https://github.com/CLIUtils/CLI11.git
    GIT_TAG v2.6.2
    OPTIONS "CLI11_BUILD_TESTS OFF" "CLI11_BUILD_EXAMPLES OFF"
)

string(
    CONCAT
    TAILGATE_BOOST_URL
    "https://github.com/boostorg/boost/releases/download/boost-1.91.0-1/"
    "boost-1.91.0-1-cmake.tar.xz"
)
CPMAddPackage(
    NAME Boost
    VERSION 1.91.0
    SYSTEM YES
    URL "${TAILGATE_BOOST_URL}"
    URL_HASH SHA256=cc5dc5006ecbdf0051f90979be31b4eee5987d9ae14ae9fb9c03cfa43fa3cdad
    DOWNLOAD_EXTRACT_TIMESTAMP ON
    EXCLUDE_FROM_ALL
    OPTIONS
        "BOOST_INCLUDE_LIBRARIES algorithm\\\;url"
)
unset(TAILGATE_BOOST_URL)

CPMAddPackage(
    NAME BoostExtDi
    VERSION 1.3.2
    GIT_REPOSITORY https://github.com/boost-ext/di.git
    GIT_TAG v1.3.2
    GIT_SHALLOW TRUE
    OPTIONS "BOOST_DI_OPT_BUILD_TESTS OFF" "BOOST_DI_OPT_BUILD_EXAMPLES OFF"
)

if(TAILGATE_BUILD_TESTS)
    CPMAddPackage(
        NAME GoogleTest
        VERSION 1.17.0
        GIT_REPOSITORY https://github.com/google/googletest.git
        GIT_TAG v1.17.0
        OPTIONS "INSTALL_GTEST OFF"
    )
endif()

if(TAILGATE_BUILD_TESTS AND TAILGATE_BUILD_UWP)
    CPMAddPackage(
        NAME Pixelmatch
        GIT_REPOSITORY https://github.com/mapbox/pixelmatch-cpp.git
        GIT_TAG 95f181d664e37751ba542e8e27006e8725ecec49
        GIT_SHALLOW TRUE
        EXCLUDE_FROM_ALL
    )
endif()

string(
    CONCAT
    TAILGATE_WIREGUARD_LWIP_URL
    "https://github.com/smartalock/wireguard-lwip/archive/"
    "c54f20dbe76ac8b3411ad21e0ed7deea6f0cfd4d.tar.gz"
)
CPMAddPackage(
    NAME WireGuardLwip
    URL "${TAILGATE_WIREGUARD_LWIP_URL}"
    URL_HASH SHA256=8be6e97394ff5d579862b9a2a3136540d6de511968235d436e83fee62cdcc9e7
    SOURCE_SUBDIR src
)
unset(TAILGATE_WIREGUARD_LWIP_URL)

# wireguard-lwip intentionally exposes source-level integration rather than a CMake target.
# Keep that upstream file-list dependency isolated here instead of leaking it into Core.
tailgate_add_third_party_library(
    tailgate_wireguard_crypto
    STATIC
    ${WireGuardLwip_SOURCE_DIR}/src/crypto.c
    ${WireGuardLwip_SOURCE_DIR}/src/crypto/refc/blake2s.c
    ${WireGuardLwip_SOURCE_DIR}/src/crypto/refc/chacha20.c
    ${WireGuardLwip_SOURCE_DIR}/src/crypto/refc/chacha20poly1305.c
    ${WireGuardLwip_SOURCE_DIR}/src/crypto/refc/poly1305-donna.c
    ${WireGuardLwip_SOURCE_DIR}/src/crypto/refc/x25519.c
    ${WireGuardLwip_SOURCE_DIR}/src/wireguard.c
)
target_include_directories(
    tailgate_wireguard_crypto
    PUBLIC
    ${WireGuardLwip_SOURCE_DIR}/src
)
target_link_libraries(
    tailgate_wireguard_crypto
    PUBLIC tailgate_core_wgengine_netstack_lwip
    PRIVATE sodium
)

set(CMAKE_C_FLAGS "${TAILGATE_ORIGINAL_C_FLAGS}")
set(CMAKE_CXX_FLAGS "${TAILGATE_ORIGINAL_CXX_FLAGS}")
unset(TAILGATE_ORIGINAL_C_FLAGS)
unset(TAILGATE_ORIGINAL_CXX_FLAGS)
